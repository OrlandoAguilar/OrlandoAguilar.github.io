<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8">
    <meta name="description" content="WebGL">
    <meta name="keywords" content="WebGL,3D,Portfolio,JavaScript,Programming,Lights,Phong-Blin,Spot,Directional,Point">
    <meta name="author" content="Orlando Aguilar Vivanco">
    
    
    <meta http-equiv="Chache-Control" content="no-cache,no-store,must-revalidate"/>
    <meta http-equiv="Pragma" content="no-cache"/>
    <meta http-equiv="Expires" content="0"/>
      
        
    <link rel="stylesheet" type="text/css" href="../../../css/main.css">
    <link rel="stylesheet" href="../Common/jquery-ui-1.11.4.custom/jquery-ui.min.css">
    <script id="vertex-shader" type="x-shader/x-vertex">
        precision mediump float;

        attribute vec4 vPosition;
        attribute vec4 vNormal;

        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;
        uniform mat3 normalMatrix;

        uniform vec4 lightPosition1;
        uniform vec4 lightPosition2;
        uniform vec4 lightPosition3;

        varying vec3 normal;
        varying vec3 eye;

        varying vec3 light1;
        varying vec3 light2;
        varying vec3 light3;

		uniform float time;
		
		struct Q{
		vec4 v;
		};
		
		struct DQ{
		Q p;
		Q q;
		};
		
		Q Conjugate(Q q){
			q.v.yzw=-q.v.yzw;
			return q;
		}
		
		Q Multiply(Q a, Q b){
			Q r;
			r.v.x=a.v.x*b.v.x-dot(a.v.yzw,b.v.yzw);
			r.v.yzw=(a.v.x*b.v.yzw+b.v.x*a.v.yzw+cross(a.v.yzw,b.v.yzw));
			return r;
		}
		
		Q Add(Q a, Q b){
			return Q(a.v+b.v);
		}
		
		Q Normalize(Q a){
			return Q(normalize(a.v));
		}
		
		DQ Conjugate(DQ dq){
			dq.p=Conjugate(dq.p);
			dq.q=Conjugate(dq.q);
			return dq;
		}
		
		DQ ConjugateDualConjugation(DQ dq){
			dq.p=Conjugate(dq.p);	
			dq.q.v.x=-dq.q.v.x;
			return dq;
		}
		
		DQ Multiply(DQ a,DQ b){
			DQ r;
			r.p=Multiply(a.p,b.p);
			r.q=Add(Multiply(a.p,b.q),Multiply(a.q,b.p));
			return r;
		}
		
		DQ Vertex2DQ(vec3 a){
			DQ r;
			r.p.v.x=1.0;
			r.q.v.yzw=a;
			return r;
		}
		
		DQ DisplacementDQ(vec3 a){
			DQ r;
			r.p.v.x=1.0;
			r.q.v.yzw=a*0.5;
			return r;
		}
		
		DQ Rotation2DQ(float angle, vec3 axis){
			DQ r;
			r.p.v.x=cos(angle*0.5);
			float s=sin(angle*0.5);
			r.p.v.yzw=axis*s;
			return r;
		}
		
		DQ Normalize(DQ a){
			float mag=dot(a.p.v,a.p.v);
			float inmag=1.0/mag;
			a.p.v*=inmag;
			a.q.v*=inmag;
			return a;
		}
		
		DQ Transformation(float rotation, vec3 axis,vec3 shift){
			
			DQ rot=Normalize(Rotation2DQ(rotation,axis));
			DQ dis=DisplacementDQ(shift);//Multiply(rot,DisplacementDQ(shift));
			DQ r=Multiply(dis,rot);
			return r;
		}
		
		vec3 Transform(DQ a, vec3 v){
			return Multiply(Multiply(a,Vertex2DQ(v)),ConjugateDualConjugation(a)).q.v.yzw;
		}
		
		bool Equal(vec3 i,vec3 b){
			return i.x==b.x && i.y==b.y && i.z==b.z;
		}
		
		DQ Power(DQ a, float r){
			float theta=2.0*acos(a.p.v.x);
			float invRt=1.0/sqrt(dot(a.p.v.yzw,a.p.v.yzw)+0.00000001);
			float d = -2.0*a.q.v.x*invRt;
			vec3 I = a.p.v.yzw*invRt;
			vec3 M = (a.q.v.yzw-I*d*a.p.v.x*0.5)*invRt;

			DQ res;
			
			float pTheta=theta*r*0.5;
			float c=r*d*0.5;
			float CPtheta=cos(pTheta);
			float SPtheta=sin(pTheta);
			
			res.p.v.x=CPtheta;
			res.p.v.yzw=I*SPtheta;
			
			res.q.v.x=-c*SPtheta;
			res.q.v.yzw=I*c*CPtheta+M*(SPtheta); //+c*CPtheta
		
			return res;
		}
		
		DQ InterpolateSlow(DQ A, DQ B, float t){
			DQ Ac=Conjugate(A);
			DQ AcB=Multiply(Ac,B);
			DQ P=Power(AcB,t);
			return Multiply(A,P);
		}
		
		
        void main()
        {
		
			DQ A = Transformation(0.0,vec3(1,0,0), vec3(-0.2,0.0,0.0));	//dual quaternion transformation	
			DQ B = Transformation(0.9,normalize(vec3(1,0,1)), vec3(0.2,0.0,0.0));	//dual quaternion transformation			
			
			DQ C = Transformation(0.0,vec3(0,1,0), vec3(0,0.0,0.0));	//dual quaternion transformation	
			DQ D = Transformation(3.14159*0.5,normalize(vec3(0,1,0)), vec3(0,0.0,0.0));	//dual quaternion transformation
			
			DQ Trf1 = InterpolateSlow(A,B,abs(sin(time)));			
			DQ Trf2 = InterpolateSlow(C,D,abs(sin(time)));
			
			DQ Trf2Global = Multiply(Trf1,Trf2);
			
			vec3 r = Transform(Trf1,vPosition.xyz);							//apply first transformation 
			vec3 s = Transform(Trf2Global,vPosition.xyz);							//apply second transformation (children of Tfr1)
			
			float weight = vPosition.y+0.5;				
			
			/*
			cylinder
			
			|....|  T1
			|....|  .
			|....|  .      lerp(T0,T1,y)
			|....|  .
			|....|  T0
			
			*/
			
			
			
			vec3 newCoordinate = mix(r,s,weight);
			
			vec4 pp=vec4(newCoordinate,1.0);

			//vec4 pp=vec4(vPosition.xyz,1.0);
			
			vec4 pos4 = (modelViewMatrix * pp);
			vec3 pos= pos4.xyz;

			light1=(lightPosition1.w < 0.0? lightPosition1.xyz : lightPosition1.xyz - pos);
			light2=(lightPosition2.w < 0.0? lightPosition2.xyz : lightPosition2.xyz - pos);
			light3=(lightPosition3.w < 0.0? lightPosition3.xyz : lightPosition3.xyz - pos);

			eye =  -normalize(pos);
			normal = normalize( normalMatrix*vNormal.xyz);
			gl_Position = projectionMatrix * pos4;

			gl_PointSize = 10.0;

        }
    </script>
    <script id="fragment-shader" type="x-shader/x-fragment">
        precision mediump float;

        varying vec3 normal;
        varying vec3 eye;

        varying vec3 light1;
        varying vec3 light2;
        varying vec3 light3;

        uniform vec3 diffuseLight1;
        uniform vec3 diffuseLight2;
        uniform vec3 diffuseLight3;
        uniform vec3 specularLight1;
        uniform vec3 specularLight2;
        uniform vec3 specularLight3;

        uniform vec3 diffuseMaterial;
        uniform vec3 specularMaterial;
        uniform float shininessMaterial;

        uniform vec4 lightPosition1;
        uniform vec4 lightPosition2;
        uniform vec4 lightPosition3;

        uniform vec3 lightConeDirection2;

        uniform int wireframe;

        uniform int activeLight0;
        uniform int activeLight1;
        uniform int activeLight2;

        float saturate(float f){
        return max(f,0.0);
        }


        vec3 Emispheric(vec3 normal,vec3 upColor,vec3 downColor){
        float fact=normal.y*0.5+0.5;    //to range 0-1
        return mix(downColor,upColor,fact);
        }

        float DecayFact(float dec){
        return 1.0/(dec*dec+dec+1.0);
        }

        vec3 Light(vec3 N, vec3 E, vec3 light,vec3 diffuseProduct,vec3 specularProduct,float shnss, float dfactor,float cone){

        vec3 L= normalize(light);
        vec3 H = normalize( L + E );

        float Kd = saturate( dot(L, N) );
        vec3  diffuse = Kd*diffuseProduct;

        vec3  specular =vec3(0.0);

        float decay=length(light.xyz);

        if( Kd >= 0.0 ){
        float Ks = pow( saturate(dot(N, H)), shnss );
        specular = Ks * specularProduct;
        }

        float attn=1.0;
        if (dfactor>0.0) attn=DecayFact(decay)*dfactor;
        
        if (cone>0.0 && dot(normalize(lightConeDirection2),-L) < cone){
        attn=0.0;
        }

        return (diffuse+specular)*attn;
        }

        void main(){

        vec3 E= normalize(eye);
        vec3 N=normalize(normal);

        vec3 ambient=Emispheric(N,vec3(0.1),vec3(0.05));

        vec3 l1=activeLight0>0? Light(N, E, light1, diffuseMaterial*diffuseLight1,specularMaterial*specularLight1,shininessMaterial, lightPosition1.w,-1.0) : vec3(0);
        vec3 l2=activeLight1>0?Light(N, E, light2, diffuseMaterial*diffuseLight2,specularMaterial*specularLight2,shininessMaterial, lightPosition2.w,0.7) : vec3(0);
        vec3 l3=activeLight2>0?Light(N, E, light3, diffuseMaterial*diffuseLight3,specularMaterial*specularLight3,shininessMaterial, lightPosition3.w,-1.0) : vec3(0);

        vec3 color=l1+l2+l3+ambient*diffuseMaterial;

        gl_FragColor = vec4(color,1);
        }
    </script>
    <script type="text/javascript" src="../Common/webgl-utils.js"></script>
    <script type="text/javascript" src="../Common/initShaders.js"></script>
    <script type="text/javascript" src="../Common/MV2.js"></script>
    <script type="text/javascript" src="../Common/Utility.js"></script>
    <script type="text/javascript" src="../Common/Models.js"></script>
    <script type="text/javascript" src="DualQuaternions.js"></script>
    <script type="text/javascript" src="../Common/jscolor/jscolor.js"></script>
    <script type="text/javascript" src="../Common/jquery-1.11.3.min.js"></script>
    <script type="text/javascript" src="../Common/jquery-ui-1.11.4.custom/jquery-ui.min.js"></script>

</head>
<body>
<!--
    <div class="header">
        <nav>
            <ul>
                <li><a href="../../../index.html">Home</a></li>
                <li><a href="../../../Resume/Resume.html">Resume</a></li>
                <li><a href="../../Portfolio.html">Portfolio</a></li>
            </ul>
        </nav>
    </div>
    <h1>DualQuaternions</h1>
-->
    <canvas id="gl-canvas" width="512" height="512">
        Oops ... your browser doesn't support the HTML5 canvas element
    </canvas>
    <div class="fieldset-auto-width">
        
        <!-- Camera controls-->
        <div class="left">
            <form>
                <fieldset class="fieldset-auto-width">
                    <legend>Camera:</legend>
                    <div>
                        <label for="radius">radius</label>
                        <input id="radius" name="radius" value="8" style="width:80px">
                    </div>

                    <div>
                        <div class="slider">
                            <label for="thetaSlider">&#928;</label>
                            <div id="thetaSlider"></div>
                        </div>
                        <div class="slider">
                            <label for="phiSlider">&#934;</label>
                            <div id="phiSlider"></div>
                        </div>
                    </div>
                </fieldset>
            </form>
        </div>
		<div class="left" >
		
		<pre>
			<code>
In this example, I have written a interpolation for the sphere and the objects around it 
(although they have an extra shift applied from javascript).
The dual quaternions are A and B, defined as angle of rotation, axis of rotation 
and displacement in the Transformation constructor below:

DQ A=Transformation(0.0,vec3(0,1,0), vec3(-1.0,0.0,0.0));		
DQ B=Transformation(0.9,normalize(vec3(1,1,0)), vec3(1.0,0.0,0.0));				

The interpolation is performed using a oscillating time.
DQ Trf=InterpolateSlow(A,B,abs(sin(time)));

The code of the shader can be found in 
			</code>
		</pre>
		
		<button class="button" onclick="$('#targetCode').toggle();">
		Code
		</button>
	
		<div id="targetCode" style="display: none" >
		<pre><code id="shaderCode" class="c++" >
		</code>
		</pre>
			<script>
			  document.getElementById('shaderCode').innerHTML = document.getElementById( 'vertex-shader' ).text;
			</script>
		</div>
	
        <!-- end camera controls-->
    </div>
	<!--
    <footer>
        <ul>
            <li><a href="mailto:orlando.aguilar@digipen.edu">email</a></li>
            <li><a href="https://github.com/OrlandoAguilar">github.com/OrlandoAguilar</a></li>
        </ul>
    </footer> -->
</body>
</html>
